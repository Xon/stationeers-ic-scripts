alias externalSensor d0
define hotSteam HASH("Hot steam tank")
define hotishSteam HASH("Hotish steam tank")
define warmWater HASH("Warm water tank")
define warmWaterValve HASH("Warn water valve")
define waterCoolerVent HASH("h20 cooling vent")
define hotSteamPump HASH("Hot steam pump")
define hotishSteamPump HASH("Hotish steam pump")
define steamCondensor HASH("Steam condensor")

define gasDigitalValve -1280984102
define poweredVent -785498334
define sGasTank 272136332
define sLiquidTank 608607718
define gasVolumePump -321403609
define condensor 1420719315
alias externalTemp r13
alias needCooling r14

loop:
yield
# check if external is lower than tanks
l externalTemp externalSensor Temperature
move needCooling 0

jal checkPumpHotToHotish
jal checkPumpHotishToWarm
jal checkWarmWaterCooling

s db Setting needCooling
sbn poweredVent waterCoolerVent Mode 1
sbn poweredVent waterCoolerVent On needCooling
j loop
checkPumpHotToHotish:
lbn r0 sGasTank hotSteam Temperature 3
ble r0 externalTemp stopPumpHotToHotish
lbn r0 sGasTank hotSteam Pressure 3
beqz r0 stopPumpHotToHotish
lbn r0 sGasTank hotishSteam Pressure 3
bgt r0 40000 stopPumpHotToHotish
move needCooling 1
sbn gasVolumePump hotSteamPump On 1
j ra
stopPumpHotToHotish:
sbn gasVolumePump hotSteamPump On 0
j ra
checkPumpHotishToWarm:
lbn r0 sGasTank hotishSteam Temperature 3
ble r0 externalTemp stopPumpHotishToWarm
lbn r0 sGasTank hotishSteam Pressure 3
beqz r0 stopPumpHotishToWarm
lbn r0 sLiquidTank warmWater Pressure 3
bgt r0 4000 stopPumpHotishToWarm
move needCooling 1
sbn gasVolumePump hotishSteamPump On 1
sbn condensor steamCondensor On 1
j ra
stopPumpHotishToWarm:
sbn gasVolumePump hotishSteamPump On 0
sbn condensor steamCondensor On 0
j ra
checkWarmWaterCooling:
lbn r0 sLiquidTank warmWater Temperature 3
ble r0 externalTemp stopWarmWaterCooling
lbn r0 sLiquidTank warmWater Pressure 3
beqz r0 stopWarmWaterCooling
move needCooling 1
sbn gasDigitalValve warmWaterValve On 1
j ra
stopWarmWaterCooling:
sbn gasDigitalValve warmWaterValve On 0
j ra