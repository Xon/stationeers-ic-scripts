##### SYSTEM VARIABLES #####
define SetPoint 14000
define MaxSetPoint 15000
define OutputSetPoint 4000
define MinSrcPressure 500
define Kp 0.006 #Aprox MaxOutput/SP
define Ti 0
define Td 0
define MinOutput 0
define MaxOutput 100
define FilterTicks 3 #SensorFilter
# Direct Mode => 1
# Reverse Mode => -1
define RegulatorMode -1

alias CompressSensor d0
alias Actuator d1
alias SrcSensor d2
alias OutputSensor d5

define TimeStep 0.5
alias InputValue r0
alias e r1
alias ProcessValue r3
alias iCalc r4
alias dCalc r5
alias FilterCounter r6
alias FilterSum r7
alias SP r8
alias PreviousE r9
alias TempValue r10

move e 0
move ProcessValue 0
move iCalc 0
move dCalc 0
move FilterCounter 0
move FilterSum 0
move PreviousE 0
move TempValue 0
move SP SetPoint
l InputValue CompressSensor Pressure
jal CalculateError
move PreviousE e
Loop:
yield
l InputValue SrcSensor Pressure
blt InputValue MinSrcPressure DrainPressure
l InputValue OutputSensor Pressure
bge InputValue OutputSetPoint DrainPressure
l InputValue CompressSensor Pressure
bge InputValue MaxSetPoint SetOff
Filter:
beqz FilterTicks Calculate
brlt FilterCounter FilterTicks 3
div TempValue FilterSum FilterCounter #filterRedu
sub FilterSum FilterSum TempValue
add FilterCounter FilterCounter 1
min FilterCounter FilterCounter FilterTicks
add FilterSum InputValue FilterSum
div InputValue FilterSum FilterCounter
Calculate:
jal CalculateError
Proportional:
mul ProcessValue Kp e #pCalc
Integrator:
breqz Ti 4
div TempValue ProcessValue Ti
add iCalc iCalc TempValue
add ProcessValue ProcessValue iCalc
Derivative:
sub dCalc e PreviousE
div dCalc dCalc TimeStep
mul dCalc dCalc Kp
mul dCalc dCalc Td
move PreviousE e
add ProcessValue ProcessValue dCalc
Limits:
min iCalc MaxOutput iCalc
max iCalc MinOutput iCalc
min ProcessValue MaxOutput ProcessValue
max ProcessValue MinOutput ProcessValue
j SetState
SetOff:
move ProcessValue MinOutput
SetState:
s db Setting ProcessValue
bdseal Actuator SetActuator
j Loop
DrainPressure:
l InputValue CompressSensor Pressure
bgtz InputValue DoDrainPressure
move ProcessValue 0
j SetOff
DoDrainPressure:
s Actuator On 1
s Actuator Setting MaxOutput
s Actuator Mode 1
s db Setting MaxOutput
j Loop
CalculateError:
sub e InputValue SP
mul e e RegulatorMode
j ra
SetActuator:
sgez r11 ProcessValue
s Actuator On r11
s Actuator Setting ProcessValue
s Actuator Mode 0
j ra