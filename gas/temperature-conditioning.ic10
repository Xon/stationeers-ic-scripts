# Using a ballast tank, keep it at the target temp
# while topping up from an external source
# external source is assumed to be very hot
alias Tank d0
alias TankCooling d1
alias OutputValve d2
alias InputReg d3
alias TankHeating d4
# celcius/kpa for temp/pressure
define outputAtMinTemperature 17
define outputAtMaxTemperature 22
define tankMinTemperature 18
define tankMaxTemperature 21
define tankMinPresssure 200
define INACTIVE 0
define WAS_HEATING 1
define WAS_COOLING 2

# reset state
s OutputValve On 0
s TankCooling On 0
s InputReg On 0
bdns TankHeating loop
s TankHeating On 0
loop:
s db Setting r0
l r0 Tank Temperature
sub r14 r0 273.15
s db Setting r14

tankColdCheck:
# if (Tank.Temperature < tankMinTemperature)
bgt r14 tankMinTemperature tankHotCheck
jal __doHeating
j done_fast

tankHotCheck:
# if (Tank.Temperature > tankMaxTemperature)
blt r14 tankMaxTemperature tankMinPressureCheck
jal __doCooling
j done_sleep

tankMinPressureCheck:
# if (Tank.Pressure < tankMinPresssure)
l r0 Tank Pressure
s db Setting r0
blt r0 tankMinPresssure tankUnderPressurized
jal __checkForOutput
jal __stopHeatingOrCooling
j done_sleep

tankUnderPressurized:
jal __checkForOutput
s InputReg On 1
yield
j tankMinPressureCheck

errorLabel:
s db error 1

__checkForOutput:
push ra
blt r14 outputAtMinTemperature closeValveDuringTempChange
bgt r14 outputAtMaxTemperature closeValveDuringTempChange
openValveDuringTempChange:
s OutputValve On 1
j __heatingCooling_prolog
closeValveDuringTempChange:
s OutputValve On 0
__heatingCooling_prolog:
pop ra
j ra

__doHeating:
push ra
move r15 WAS_HEATING
s TankCooling On 0
bdns TankHeating heatViaInput
s TankHeating On 1
j __doHeating_prolog
heatViaInput:
s InputReg On 1
__doHeating_prolog:
jal __checkForOutput
pop ra
j ra

__doCooling:
push ra
move r15 WAS_COOLING
s TankCooling On 1
s InputReg On 0
bdns TankHeating __doCooling_prolog
s TankHeating On 0
__doCooling_prolog:
jal __checkForOutput
pop ra
j ra

__stopHeatingOrCooling:
push ra
move r15 INACTIVE
s TankCooling On 0
s InputReg On 0
bdns TankHeating __stopHeatingOrCooling_prolog
s TankHeating On 0
__stopHeatingOrCooling_prolog:
pop ra
j ra

done_fast:
yield
j loop
done_sleep:
sleep 1
j loop
